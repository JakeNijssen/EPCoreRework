plugins {
    id 'java'
    id 'maven-publish'
    id("com.github.johnrengelman.shadow") version "7.1.2"
}

group = 'net.emeraldprison'
version = '1.1.0'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }

    maven {
        url "https://wolfeiii.com/releases"
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.19-R0.1-SNAPSHOT'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

    implementation 'com.zaxxer:HikariCP:5.0.1'

    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}


tasks {
    shadowJar {
        relocate 'com.zaxxer', 'net.emeraldprison.epcore.libs.com.zaxxer'
    }

    build {
        dependsOn(shadowJar)
    }
}

publishing {
    repositories {
        maven {
            url myMavenRepoWriteUrl
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
